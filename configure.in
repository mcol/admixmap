AC_INIT([genepi], [0.6])
AC_CANONICAL_SYSTEM
AM_INIT_AUTOMAKE([gnu])
AM_CONFIG_HEADER(config.h)

AC_LANG(C++)

dnl Shared library needs to be disabled because otherwise:
dnl /usr/lib64/gcc-lib/x86_64-suse-linux/3.3.3/../../../../x86_64-suse-linux/bin/ld:
dnl /usr/local/mpich/path/lib64/libpmpich++.a(intercepts.o): relocation
dnl R_X86_64_32S can not be used when making a shared object; recompile with
dnl -fPIC
dnl /usr/local/mpich/path/lib64/libpmpich++.a: could not read symbols: Bad
dnl value
dnl collect2: ld returned 1 exit status
dnl make[4]: *** [libbayes-para.la] Error 1
dnl 
dnl ...so:
dnl AC_DISABLE_SHARED

AC_PROG_CXX
AC_DISABLE_SHARED
AC_PROG_LIBTOOL
AC_PROG_INSTALL
AC_PROG_MAKE_SET
AC_PROG_YACC
AC_PROG_LEX


AC_HEADER_STDC
AC_LANG_CPLUSPLUS

AC_CHECK_LIB(stdc++, main,,AC_MSG_ERROR(libstdc++ required))
AM_PATH_GSL
AC_CHECK_HEADERS(stack,,AC_MSG_WARN(STL classes missing ?))
AC_CHECK_HEADERS(string,,AC_MSG_WARN(STL classes missing ?))
AC_CHECK_HEADERS(list,,AC_MSG_WARN(STL classes missing ?))
AC_CHECK_HEADERS(vector,,AC_MSG_WARN(STL classes missing ?))

AC_CHECK_PROGS([BASH], [bash], AC_MSG_ERROR(This package needs Bourne shell.))

AC_ARG_ENABLE(static-binary,[  --enable-static-binary  Build a static binary],
	      [case "${enableval}" in
	       yes) static=true ;;
	       no) static=false ;;
	       *) AC_MSG_ERROR(bad value ${enableval} for --enable-static) ;;
	       esac], [static=dunno])

AC_ARG_ENABLE(parallel, [  --enable-parallel       Build the parallel version],
	      [case "${enableval}" in
	       yes) parallel=true ;;
	       no) parallel=false ;;
	       *) AC_MSG_ERROR(bad value ${enableval} for --enable-parallel) ;;
	       esac], [parallel=false])

AM_CONDITIONAL(PARALLEL, test x$parallel = xtrue)

enable_static_binary="no"

if test x$parallel = xtrue; then
	enable_parallel="yes"
	AC_CHECK_HEADERS([mpi++.h],,AC_MSG_ERROR(MPI headers are missing))
	PARALLEL_LIBS="-lmpich -lgmp -lsprng -lmpe"
	PARALLEL_ENABLE="yes"
	dnl FIXME: Check MPI libraries
	dnl AC_CHECK_LIB(mpic, main,,AC_MSG_ERROR(hapmixmap requires libstdc++)) 
else
	if test x$static != xfalse; then
		enable_static_binary="yes"
	fi
	enable_parallel="no"
	PARALLEL_LIBS=""
	PARALLEL_ENABLE="no"
fi


AC_SUBST([PARALLEL_LIBS])
AC_SUBST([PARALLEL_ENABLE])

if test "$enable_static_binary" = "yes"; then
	STATIC_LDFLAGS="-all-static"
else
	STATIC_LDFLAGS=""
fi

AC_SUBST([STATIC_LDFLAGS])

if test "$enable_parallel" = "yes" -a "$enable_static_binary" = "yes"; then
	AC_MSG_ERROR(Can't build a static parallel version.)
fi

AC_CONFIG_FILES([src/scripts/genepi-config])

## write definitions of compiler, flags etc in config.h
AC_DEFINE_UNQUOTED([COMPILER],["${CXX}"],[C++ compiler]) 
AC_DEFINE_UNQUOTED([COMPILER_FLAGS],["${CXXFLAGS}"],[C++ compiler flags]) 
AC_DEFINE_UNQUOTED([HOST],["${host}"],[For platform]) 
AC_DEFINE_UNQUOTED([TARGET],["${target}"],[Built on]) 


dnl AC_CONFIG_SUBDIRS(src/bayeslib)
AC_OUTPUT([Makefile
	   src/Makefile
	   src/admixmap/Makefile
	   src/hapmixmap/Makefile
	   src/common/Makefile
	   src/bayeslib/Makefile
	   src/bayeslib/regression/Makefile
	   src/bayeslib/samplers/Makefile
	   src/bayeslib/utils/Makefile
	   src/scripts/Makefile
	   src/tools/Makefile
           src/tools/hapmixmapFormatter/Makefile
           src/tools/ancestrymapConversion/Makefile
	   test/Makefile
	   test/admixmap/Makefile
	   dist/Makefile
	   dist/admixmap/Makefile
	   dist/hapmixmap/Makefile
	   dist/hapmixmap/data/Makefile
	   ])

echo \
"------------------------------------------------------------------------
Configuration:

  Source code location:  ${srcdir}
  C Compiler:            ${CC}
  C++ Compiler:          ${CXX}
  Compiler flags:        ${CXXFLAGS}
  Host System Type:      ${host}
  Install path:          ${prefix}
  Parallel version:      ${enable_parallel}
  Static binary:         ${enable_static_binary}

  See config.h for further configuration information.
------------------------------------------------------------------------"


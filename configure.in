AC_INIT([genepi], [0.4-nst2])
AC_CANONICAL_SYSTEM
AM_INIT_AUTOMAKE([gnu])
AM_CONFIG_HEADER(config.h)

AC_LANG(C++)

dnl Shared library needs to be disabled because otherwise:
dnl /usr/lib64/gcc-lib/x86_64-suse-linux/3.3.3/../../../../x86_64-suse-linux/bin/ld:
dnl /usr/local/mpich/path/lib64/libpmpich++.a(intercepts.o): relocation
dnl R_X86_64_32S can not be used when making a shared object; recompile with
dnl -fPIC
dnl /usr/local/mpich/path/lib64/libpmpich++.a: could not read symbols: Bad
dnl value
dnl collect2: ld returned 1 exit status
dnl make[4]: *** [libbayes-para.la] Error 1
dnl 
dnl ...so:
dnl AC_DISABLE_SHARED

AC_PROG_CXX
AC_DISABLE_SHARED
AC_PROG_LIBTOOL
AC_PROG_INSTALL
AC_PROG_MAKE_SET
AC_PROG_YACC
AC_PROG_LEX


AC_HEADER_STDC
AC_LANG_CPLUSPLUS

AC_CHECK_LIB(stdc++, main,,AC_MSG_ERROR(libstdc++ required))
AM_PATH_GSL
AC_CHECK_HEADERS(stack,,AC_MSG_WARN(STL classes missing ?))
AC_CHECK_HEADERS(string,,AC_MSG_WARN(STL classes missing ?))
AC_CHECK_HEADERS(list,,AC_MSG_WARN(STL classes missing ?))
AC_CHECK_HEADERS(vector,,AC_MSG_WARN(STL classes missing ?))

AC_CHECK_PROGS([BASH], [bash], AC_MSG_ERROR(This package needs Bourne shell.))

AC_ARG_ENABLE(static-binary,[  --enable-static-binary  Build a static binary],
	      [case "${enableval}" in
	       yes) static=true ;;
	       no) static=false ;;
	       *) AC_MSG_ERROR(bad value ${enableval} for --enable-static) ;;
	       esac], [static=dunno])

AC_ARG_ENABLE(parallel, [  --enable-parallel       Build the parallel version],
	      [case "${enableval}" in
	       yes) parallel=true ;;
	       no) parallel=false ;;
	       *) AC_MSG_ERROR(bad value ${enableval} for --enable-parallel) ;;
	       esac], [parallel=false])

AM_CONDITIONAL(PARALLEL, test x$parallel = xtrue)

AC_ARG_ENABLE(converter, [  --enable-converter     Build the data converter],
	      [case "${enableval}" in
	       yes) converter=true ;;
	       no) converter=false ;;
	       *) AC_MSG_ERROR(bad value ${enableval} for --enable-converter) ;;
	       esac], [converter=false])

AM_CONDITIONAL(CONVERTER, test x$converter = xtrue)

enable_static_binary="no"

if test x$parallel = xtrue; then
	enable_parallel="yes"
	AC_CHECK_HEADERS([mpi++.h],,AC_MSG_ERROR(MPI headers are missing))
	PARALLEL_LIBS="-lmpich -lgmp -lsprng -lmpe"
	PARALLEL_ENABLE="yes"
	dnl FIXME: Check MPI libraries
	dnl AC_CHECK_LIB(mpic, main,,AC_MSG_ERROR(hapmixmap requires libstdc++)) 
else
	if test x$static != xfalse; then
		enable_static_binary="yes"
	fi
	enable_parallel="no"
	PARALLEL_LIBS=""
	PARALLEL_ENABLE="no"
fi


AC_SUBST([PARALLEL_LIBS])
AC_SUBST([PARALLEL_ENABLE])

AC_ARG_ENABLE(testing, [  --enable-testing        Build unit-tests with CppUnit], 
 		              [case "${enableval}" in 
 		               yes) unit_test=true ;; 
 		               no) unit_test=false ;; 
 		               *) AC_MSG_ERROR(bad value ${enableval} for --enable-testing) ;; 
 		               esac], [unit_test=false]) 

AM_CONDITIONAL(UNIT_TEST, test x$unit_test = xtrue)

if test x${unit_test} = xtrue; then
	dnl AC_CHECK_HEADERS([cppunit/TestFixture.h],,AC_MSG_ERROR(CppUnit headers are missing))
	dnl AC_CHECK_LIB(cppunit, Test::Test,,AC_MSG_ERROR(Testing requires CppUnit))
	AM_PATH_CPPUNIT(1.10.2)
	AC_DEFINE([HAVE_CPPUNIT], [], [CppUnit testing framework, see http://cppunit.sf.net/])
fi

if test "$enable_static_binary" = "yes"; then
	STATIC_LDFLAGS="-all-static"
else
	STATIC_LDFLAGS=""
fi

AC_SUBST([STATIC_LDFLAGS])

if test "$enable_parallel" = "yes" -a "$enable_static_binary" = "yes"; then
	AC_MSG_ERROR(Can't build a static parallel version.)
fi

AC_CONFIG_FILES([src/scripts/genepi-config])

dnl AC_CONFIG_SUBDIRS(src/bayeslib)
AC_OUTPUT([Makefile
	   src/Makefile
	   src/common/Makefile
	   src/admixmap/Makefile
	   src/admixmap/interfaces/Makefile
	   src/hapmixmap/Makefile
	   src/test/Makefile
	   src/test/mock/Makefile
	   src/bayeslib/Makefile
	   src/bayeslib/regression/Makefile
	   src/bayeslib/samplers/Makefile
	   src/bayeslib/utils/Makefile
	   src/scripts/Makefile
	   src/tools/Makefile
	   dist/Makefile
	   dist/admixmap/Makefile
	   dist/hapmixmap/Makefile
	   dist/hapmixmap/data/Makefile
	   ])

echo \
"------------------------------------------------------------------------
Configuration:

  Source code location:  ${srcdir}
  C Compiler:            ${CC}
  C++ Compiler:          ${CXX}
  Compiler flags:        ${CXXFLAGS}
  Host System Type:      ${host}
  Install path:          ${prefix}
  Parallel version:      ${enable_parallel}
  Static binary:         ${enable_static_binary}
  Unit-test:             ${unit_test}
  User data converter:   ${converter}

  See config.h for further configuration information.
------------------------------------------------------------------------"
